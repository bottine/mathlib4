/-
Copyright (c) 2022 RÃ©mi Bottinelli. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: RÃ©mi Bottinelli
-/
import Mathlib.CategoryTheory.Groupoid
import Mathlib.CategoryTheory.Groupoid.Basic
import Mathlib.CategoryTheory.PathCategory
import Mathlib.CategoryTheory.Quotient
import Mathlib.CategoryTheory.Groupoid.Init


/-!
# Universal Groupoid

This file defines the universal groupoid given by a pair consisting of a groupoid and a map whose
domain is the object type of the groupoid.

-/

namespace CategoryTheory
namespace Groupoid
namespace Universal

variable {V V' : Type _} [Groupoid V] (Ïƒ : V â†’ V')

scoped postfix:50 " * " => fun Ïƒ => Quiver.Push.of Ïƒ â‹™q Paths.of

@[simp]
def _root_.Quiver.Path.asHom {X Y : Quiver.Push Ïƒ} (f : Quiver.Path X Y) :
    Paths.of.obj X âŸ¶ Paths.of.obj Y := f

@[simp]
def _root_.Quiver.Hom.push {X Y : V} (f : X âŸ¶ Y) : (Ïƒ *).obj X âŸ¶ (Ïƒ *).obj Y := (Ïƒ *).map f

@[simp]
lemma PathsPush_id_eq (X : Paths $ Quiver.Push Ïƒ) : ğŸ™ X = Quiver.Path.nil := rfl

@[simp]
lemma PathsPush_comp_eq {X Y Z : Paths $ Quiver.Push Ïƒ} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
  f â‰« g = Quiver.Path.comp f g := rfl

@[simp]
def _root_.Quiver.Hom.rev {Ïƒ : V â†’ V'} {X Y : Paths $ Quiver.Push Ïƒ} (f : X âŸ¶ Y) : Y âŸ¶ X :=
  f.reverse.asHom

scoped infixl:100 " â€  " => Quiver.Hom.push

/-- Two reduction steps possible:Â compose composable arrows, or drop identity arrows -/
inductive red.atomic_step : HomRel (Paths (Quiver.Push Ïƒ))
  /-- Pushing a composite is the same as composing the pushes -/
  | comp (X Y Z : V) (f : X âŸ¶ Y) (g : Y âŸ¶ Z) :
      red.atomic_step (Ïƒ â€  f â‰« Ïƒ â€  g) (Ïƒ â€  (f â‰« g))
  /-- Pushing the id is the id path -/
  | id (X : V) :
      red.atomic_step (Ïƒ â€  ğŸ™ X) (ğŸ™ $ (Ïƒ *).obj X) -- ugly

@[simp]
def red.step {X Y : Paths $ Quiver.Push Ïƒ} (p q : X âŸ¶ Y) :=
  Quotient.CompClosure (red.atomic_step Ïƒ) p q

abbrev red.step' (Ïƒ : V â†’ V') {X Y : Paths $ Quiver.Push Ïƒ} (p q : X âŸ¶ Y) :=
  @red.step _ _ _ Ïƒ X Y p q

lemma red.atomic_step.reverse : {X Y : Paths $ Quiver.Push Ïƒ} â†’ (f g : X âŸ¶ Y) â†’
  red.atomic_step Ïƒ f g â†’ red.atomic_step Ïƒ f.rev g.rev
  | _, _, _, _, .comp X Y Z f g => by
    simp [Quiver.Push.of_obj, Quiver.Path.reverse, â†Prefunctor.map_reverse, reverse_eq_inv,
               inv_eq_inv, Quiver.Path.comp_nil, IsIso.inv_comp, Quiver.Hom.rev]
    apply red.atomic_step.comp
  | _, _, _, _, .id X => by
    simp only [Quiver.Push.of_obj, Quiver.Path.reverse, â†Prefunctor.map_reverse, reverse_eq_inv,
               inv_eq_inv, IsIso.inv_id, Quiver.Path.comp_nil, Quiver.Hom.rev]
    apply red.atomic_step.id X

/-- The underlying vertices of the Universal Groupoid -/
def _root_.CategoryTheory.Groupoid.UniversalGroupoid := Quotient (red.atomic_step Ïƒ)

instance : Category (UniversalGroupoid Ïƒ) := Quotient.category (red.atomic_step Ïƒ)

lemma red.step.reverse : {X Y : Paths $ Quiver.Push Ïƒ} â†’ (p q : X âŸ¶ Y) â†’
    red.step Ïƒ p q â†’ red.step Ïƒ (p.reverse) (q.reverse)
  | A, B, _, _, .intro f _ _ g hr => by
    convert Quotient.CompClosure.intro (g.rev) _ _ (f.rev) hr.reverse
    Â· simp
    Â· simp

lemma Quot_mk_self_comp_reverse {X} : âˆ€ {Y : Paths $ Quiver.Push Ïƒ} (p : X âŸ¶ Y),
    Quot.mk (red.step' Ïƒ) (p â‰« p.rev) = Quot.mk (red.step' Ïƒ) (ğŸ™ X)
  | _, .nil => by simp
  | _, .cons p âŸ¨eâŸ© => by
    let pp := p.asHom
    let pr := pp.rev
    calc Quot.mk (red.step' Ïƒ) ((p.cons _).asHom â‰« Quiver.Hom.rev (p.cons âŸ¨eâŸ©).asHom)
       = Quot.mk _ (p.asHom â‰« (Ïƒ â€  e) â‰« (Ïƒ â€  e).rev â‰« pr) := by
          congr 1
          simp only [Paths.of_obj, Quiver.Path.asHom, Quiver.Hom.rev, Quiver.Path.reverse,
                     Quiver.Hom.toPath,PathsPush_comp_eq, Prefunctor.comp_obj, Quiver.Push.of_obj,
                     Quiver.Hom.push, Prefunctor.comp_map, Paths.of_map, Quiver.Path.comp_nil,
                     Quiver.Path.cons_comp, Quiver.Path.nil_comp, Quiver.Path.comp_assoc]
          rfl
     _ = Quot.mk _ (pp â‰« ((Ïƒ â€  e) â‰« (Ïƒ â€  e).rev) â‰« pr) := by
          simp
     _ = Quot.mk _ (pp â‰« (Ïƒ â€  (ğŸ™ _)) â‰« pr) := by
          apply Quot.sound (Quotient.CompClosure.intro _ _ _ _ _)
          convert @red.atomic_step.comp _ _ _ Ïƒ _ _ _ e (inv e)
          simp only [inv_eq_inv, IsIso.hom_inv_id]
     _ = Quot.mk _ (pp â‰« ğŸ™ _ â‰« pr) :=
          Quot.sound (Quotient.CompClosure.intro _ _ _ _ $ @red.atomic_step.id _ _ _ Ïƒ _)
     _ = Quot.mk _ (pp â‰« pr) := by
           simp only [Paths.of_obj, Quiver.Path.asHom, PathsPush_id_eq, Quiver.Hom.rev,
                      PathsPush_comp_eq, Quiver.Path.nil_comp]
     _ = Quot.mk _ (ğŸ™ _) := Quot_mk_self_comp_reverse p

lemma Quot_mk_reverse_comp_self {X Y : Paths $ Quiver.Push Ïƒ} (p : X âŸ¶ Y) :
    Quot.mk (red.step' Ïƒ) (p.rev â‰« p) = Quot.mk (red.step' Ïƒ) (ğŸ™ Y) := by
  simpa using Quot_mk_self_comp_reverse Ïƒ p.rev


/-- The inverse of an arrow in the Universal Groupoid -/
def Quot_inv {X Y : UniversalGroupoid Ïƒ} (f : X âŸ¶ Y) : Y âŸ¶ X :=
  Quot.liftOn f
            (fun pp â†¦ Quot.mk _ $ pp.rev)
            (fun _ _ con â†¦ Quot.sound $ red.step.reverse Ïƒ _ _ con)

instance : Groupoid (UniversalGroupoid Ïƒ) where
  inv      f := Quot_inv Ïƒ f
  inv_comp f := Quot.inductionOn f $ fun pp â†¦ Quot_mk_reverse_comp_self Ïƒ pp
  comp_inv f := Quot.inductionOn f $ fun pp â†¦ Quot_mk_self_comp_reverse Ïƒ pp

/-- The extension of `Ïƒ` to a functor -/
def extend : V â¥¤ (UniversalGroupoid Ïƒ) where
  obj X := âŸ¨Ïƒ XâŸ©
  map f := Quot.mk _ (Ïƒ â€  f)
  map_id X := Quot.sound $ Quotient.CompClosure.of _ _ _ (red.atomic_step.id X)
  map_comp f g := Eq.symm $ Quot.sound $
    Quotient.CompClosure.of _ _ _ (red.atomic_step.comp _ _ _ f g)

lemma extend_eq : (extend Ïƒ).toPrefunctor =
  Quiver.Push.of Ïƒ â‹™q Paths.of â‹™q (Quotient.functor $ red.atomic_step Ïƒ).toPrefunctor := rfl

abbrev as : UniversalGroupoid Ïƒ â†’ V' := Quotient.as

section ump

variable {V'' : Type _} [Groupoid V''] (Î¸ : V â¥¤ V'') (Ï„â‚€ : V' â†’ V'') (hÏ„â‚€ : âˆ€ x, Î¸.obj x = Ï„â‚€ (Ïƒ x))

/--
Any functor `Î¸` from `V` to a Groupoid `V''` with `Î¸.obj` factoring through `Ïƒ`
defines a functor from `V'`.
 -/
noncomputable def lift : UniversalGroupoid Ïƒ â¥¤ V'' :=
Quotient.lift _
  ( Paths.lift $ Quiver.Push.lift Ïƒ Î¸.toPrefunctor Ï„â‚€ hÏ„â‚€ )
  ( fun _ _ _ _ h => by
      induction h <;>
      aesop_cat (add norm unfold [Quiver.Push.of, Quiver.Hom.toPath, Quiver.Push.lift, Paths.lift]))

lemma lift_spec_obj : (lift Ïƒ Î¸ Ï„â‚€ hÏ„â‚€).obj = Ï„â‚€ âˆ˜ as Ïƒ := rfl

lemma lift_spec_comp : extend Ïƒ â‹™ lift Ïƒ Î¸ Ï„â‚€ hÏ„â‚€ = Î¸ := by
  rw [Functor.toPrefunctor_ext,â†Functor.toPrefunctor_comp, extend_eq, lift,
      Prefunctor.comp_assoc, Functor.toPrefunctor_comp, Quotient.lift_spec,
      Prefunctor.comp_assoc, Paths.lift_spec, Quiver.Push.lift_comp]

lemma lift_unique (Î¦ : UniversalGroupoid Ïƒ â¥¤ V'')
    (Î¦â‚€ : Î¦.obj = Ï„â‚€ âˆ˜ as Ïƒ) (Î¦c : extend Ïƒ â‹™ Î¦ = Î¸) : Î¦ = lift Ïƒ Î¸ Ï„â‚€ hÏ„â‚€ := by
  apply Quotient.lift_unique
  apply Paths.lift_unique
  apply Quiver.Push.lift_unique
  Â· ext
    simp [Î¦â‚€]
  Â· simpa only [Functor.toPrefunctor_ext, â†Functor.toPrefunctor_comp] using Î¦c

end ump

end Universal
end Groupoid
end CategoryTheory
